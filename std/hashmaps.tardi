uses: std/scanning
uses: std/vectors
uses: std/_hashmaps

exports:
    <hashmap> >hashmap >vector add! each empty? get H{ in? is-hashmap? keys
    length map set! values inc ;

/// hashmap literals:
/// H{ { "key1" 4 } { "key2" 5 } }
MACRO: H{ } scan-object-list >hashmap over vectors/push! ;

/// ( hashmap -- vector )
: keys   >vector [ first ] map ;

/// ( hashmap -- vector )
: values   >vector [ second ] map ;

/// ( key hashmap -- ? )
: in?   keys vectors/in? ;

// ( hashmap lambda:( k v -- ) -- )
: each   swap >vector [ dup vectors/empty? ! ] [
      [
        vectors/pop!
        [ first ] keep
        second
        rot
        [ apply ] keep
      ] keep
    ] while 2drop ;

// ( hashmap lambda:( v -- v' ) -- hashmap' )
: map   swap >vector [
        [ second ] keep first
        [ swap [ apply ] keep ] dip
        rot
        { } rot over push!
        swap over push!
    ] vectors/map
    >hashmap nip ;

// ( hashmap -- ? )
: empty?   _hashmaps/length 0 == ;

/// hm x --
: inc
    swap 2dup get
    [ 1+ ] [ drop 1 ] if
    swap set! ;
